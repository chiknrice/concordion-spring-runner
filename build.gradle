description = 'A ConcordionRunner fused with SpringJUnit4ClassRunner '

group 'org.chiknrice'

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.10', 'org.concordion:concordion:1.5.1'
    compile 'org.springframework:spring-context:4.1.7.RELEASE'
    compile 'org.springframework:spring-test:4.1.7.RELEASE'
    testCompile 'junit:junit:4.11'
    testRuntime 'org.slf4j:slf4j-simple:1.7.10'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1'
        classpath 'net.researchgate:gradle-release:2.2.1'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
    }
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'net.researchgate.release'

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['Release']
    pkg {
        repo = 'maven'
        name = 'concordion-spring-runner'
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/chiknrice/concordion-spring-runner.git'
    }
}

def pomConfig = {
    name project.name
    url "https://github.com/chiknrice/${project.name}"
    scm {
        url "https://github.com/chiknrice/concordion-spring-runner"
        connection "scm:git:git@github.com:chiknrice/concordion-spring-runner.git"
        developerConnection "scm:git:git@github.com:chiknrice/concordion-spring-runner.git"
    }
    licenses {
        license {
            name 'GPL v3.0'
            url 'http://www.gnu.org/licenses/gpl.txt'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id 'chiknrice'
            name 'Ian Bondoc'
            email 'chiknrice@gmail.com'
            url 'https://github.com/chiknrice'
        }
    }
}

publishing {
    publications {
        Release(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                asNode().appendNode("description", project.description).plus(pomConfig)
            }
        }
    }
}

release {
    tagTemplate = 'v${version}'
}

test {
    systemProperties['concordion.output.dir'] = "$reporting.baseDir/spec"
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}